class Environment is subclass of GLOBAL

types 

Input = (CustomerChoice * seq of ItemTimeInline * seq of BoxInline);

ItemPropertyInline = (RecycleMark * Barcode * Shape);
ItemInline = (Identifier * ItemPropertyInline);
ItemTimeInline	= (Time * ItemInline);

BoxPropertyInline = (Identifier * Shape * nat * map ItemInline to nat);
BoxInline  = (Time * BoxPropertyInline);
RecycleInline = ItemTimeInline | BoxInline;

Outline = (Time * CustomerChoice * real);

public RecycleEntry ::
	time : nat
	recycle : Recycle;

instance variables
 
io : IO := new IO();

busy : bool := true;

outlines : seq of Outline := [];

cusTime : CustomerChoice;

singleItems : seq of RecycleEntry := [];
boxes : seq of RecycleEntry := [];

operations 

public Environment : seq of char  ==> Environment
Environment(fname) ==
	def mk_(-,mk_(choice, itemInput, boxInput)) = io.freadval[Input](fname)
	in (
	singleItems := [MakeRecycle(x) | x in seq itemInput];
	boxes := [MakeRecycle(x) | x in seq boxInput];
	cusTime := choice);
	
private CreateSignal : () ==> ()
CreateSignal() ==
	(if len singleItems > 0 or len boxes > 0 
	then	
		(dcl 
			curTime : Time := World`timerRef.GetTime(),
			boxDone : bool := false,
			itemDone : bool := false;
		busy := true;
		while not boxDone and not itemDone 
		do
			(if len singleItems > 0
			then
				(let 
					item = hd singleItems
				in 
					if item.time <= curTime
					then
						(World`rm.singleItemConveyor.InputRecycle(item.recycle);
						singleItems := tl singleItems;
						itemDone := len singleItems = 0)
					else itemDone := true)
			else itemDone := true;
			if len boxes > 0 
			then
				(let 
					box = hd boxes
				in 
					if box.time <= curTime
					then
						(World`rm.boxConveyor.InputRecycle(box.recycle);
						boxes := tl boxes;
						boxDone := len boxes = 0;)
					else boxDone := true)
			else boxDone := true))
	else 
		busy := false);

public HandleEvent : Time * CustomerChoice * real ==> ()
HandleEvent(pt, pcc, pa) ==
	outlines := outlines ^ [mk_(pt, pcc, pa)];
	
public HandleEvent : RecycleMachineState ==> ()
HandleEvent(prms) ==
	cases prms: 
		<ItemInvalid> -> def -= World`rm.singleItemConveyor.RemoveRecycle() in skip,
		
		<BoxInvalid> -> def -= World`rm.boxConveyor.RemoveRecycle() in skip,
		
		<TooManyItems> -> (dcl rec : Recycle := World`rm.singleItemConveyor.RemoveRecycle();
											singleItems := singleItems  ^ [mk_RecycleEntry(GetNewTime(singleItems, World`timerRef), rec)]),
											
		<TooManyBoxes> -> (dcl rec : Recycle := World`rm.boxConveyor.RemoveRecycle();
											boxes := boxes  ^ [mk_RecycleEntry(GetNewTime(boxes, World`timerRef), rec)]),
											
		<ItemStorageFull> -> (dcl rec : Recycle := World`rm.singleItemConveyor.RemoveRecycle();
											singleItems := singleItems  ^ [mk_RecycleEntry(GetNewTime(singleItems, World`timerRef), rec)];
											World`rm.storage.ClearItemStorage()),
											
		<BoxStorageFull> -> (dcl rec : Recycle := World`rm.boxConveyor.RemoveRecycle();
											boxes := boxes  ^ [mk_RecycleEntry(GetNewTime(boxes, World`timerRef), rec)];
											World`rm.storage.ClearBoxStorage())
	end;
	
private IsFinished : () ==> bool
IsFinished() ==
	return boxes = [] and singleItems = [] and not busy;

public ShowResult : () ==> ()
ShowResult() ==
	def -= io.writeval[seq of Outline](outlines) in skip;

public Run : () ==> ()
Run() ==
	(
	while not (IsFinished()
						and
						World`rm.recycleMachineController.IsFinished()
						and
						World`rm.singleItemConveyor.IsFinished()
						and
						World`rm.boxConveyor.IsFinished())
	do
		(
		CreateSignal();
		World`rm.recycleMachineController.Step();
		World`rm.singleItemConveyor.Step();
		World`rm.boxConveyor.Step();
		World`timerRef.StepTime();
		);
  if cusTime = <Donate>
  then World`rm.ui.Donate() 
  else World`rm.ui.GetReceipt();
	);

functions

private MakeRecycle : RecycleInline -> RecycleEntry
MakeRecycle(rec) ==
	if is_Shape(rec.#2.#2)
	then 
		mk_RecycleEntry(rec.#1, mk_Box(rec.#2.#1, rec.#2.#2, rec.#2.#3, CreateMapping(rec.#2.#4)))
	else 
		mk_RecycleEntry(rec.#1, mk_Item(rec.#2.#1, mk_ItemProperties(rec.#2.#2.#1, rec.#2.#2.#2, rec.#2.#2.#3)));

private CreateMapping : map ItemInline to nat -> map Item to nat
CreateMapping(pmiitn) == 
if pmiitn = {|->}
then {|->}
else 
	let item in set dom pmiitn
	in
		let pos = pmiitn(item)
		in
		({mk_Item(item.#1, mk_ItemProperties(item.#2.#1, item.#2.#2, item.#2.#3)) |-> pos} 
		munion CreateMapping({item} <-: pmiitn))
measure Card;

private Card : map ItemInline to nat  -> nat
Card(pmiitn) == 
	card dom pmiitn;
	
private GetNewTime : seq of RecycleEntry * Timer -> nat
GetNewTime(psre, pt) == 
	if psre <> []
	then psre(len psre).time + 10
	else pt.GetTime() + 10; 

end Environment