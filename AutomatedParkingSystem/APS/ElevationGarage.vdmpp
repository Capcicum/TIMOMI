class ElevationGarage is subclass of GLOBAL

types

public GateState = [<Open> | <Closed>];

instance variables
	EntranceGate : GateState := <Open>;
	ExitGate : GateState := <Closed>;
	parkedCar : [Car] := nil; 
	isFree : bool := true;

operations
	
public parkCar : Car ==> ()
parkCar(pcar) == (
	parkedCar := pcar;
	isFree := false;)
pre parkedCar = nil;	
	
public handleParking:  PaymentInfo ==> ()
handleParking(ppi) == (	
	if not isFree
		then (
				 if APS`validationService.validateLicensePlate(parkedCar.licensePlate) and 
				 APS`validationService.validatePaymentInfo(ppi,parkedCar.parkingPermit)
				 		then (garageBusy();
				 					APS`informationStation.registerCar(parkedCar.licensePlate,ppi,World`timeRef.getTime());
				 					APS`parkingLevel.parkCar(parkedCar);
				 					printParkingReceipt(ppi);
				 					APS`io.print("STATUS: garageDB contains: ");APS`io.print(APS`garageDB); APS`io.print("\n");
				 					parkedCar := nil;
				 					garageFree();)
				 else 
				 (garageReadyForExit();	 
				 APS`io.print("STATUS: Error - <ValidationFailed>\n"); 
				 parkedCar := nil;
				 garageFree();)
				 )
)
pre EntranceGate = <Open> and ExitGate = <Closed> and isFree = false; 
		
public retriveCar: LicensePlate ==> () 
retriveCar(plp) == (
	isFree := false;
	garageBusy();
	parkedCar := APS`parkingLevel.retriveCar(plp);
	garageReadyForExit();
	parkedCar := nil;
	APS`informationStation.unRegisterCar(plp);
	garageFree();
	isFree := true)
pre EntranceGate = <Open> and ExitGate = <Closed> and 
		isFree = true and APS`garageDB <> {} ; 
		
pure public isGarageFree: () ==> bool
isGarageFree() == 
	return isFree;
	
public garageFree: () ==> ()
garageFree() == (
	if EntranceGate = <Closed>
		 then EntranceGate := <Open>;
	if ExitGate = <Open>
		 then ExitGate := <Closed>;
	isFree := true;)
pre EntranceGate = ExitGate and isFree = false;

public garageBusy: () ==> ()
garageBusy() == (
	EntranceGate := <Closed>;)
pre EntranceGate = <Open> and ExitGate = <Closed> and isFree = false;

public garageReadyForExit: () ==> ()
garageReadyForExit() == (
	if EntranceGate = <Closed>
		 then EntranceGate := <Open>;
	ExitGate := <Open>;)
pre ExitGate = <Closed> and isFree = false;

public getGateStates: () ==> seq of GateState
getGateStates() ==
	return [EntranceGate, ExitGate];

public printParkingReceipt : PaymentInfo ==> ()
printParkingReceipt(ppi) == (
		APS`io.print("---------------------\n"); 
		APS`io.print("  Parking Receipt  \n"); 
		APS`io.print("Car: "); APS`io.print(parkedCar); APS`io.print("\n");
		APS`io.print("Current time: "); APS`io.print(World`timeRef.getTime()); APS`io.print("\n");
		APS`io.print("Payment method: ");
		if is_ParkingCard(ppi)
			then APS`io.print("Parking Card\n")
		else APS`io.print("Credit Card\n");
		APS`io.print("---------------------\n"););

end ElevationGarage